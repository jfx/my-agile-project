*** Settings ***
Documentation     In order to see details about a specific domain
...               As a connected user
...               I need to view domain details.
Resource          ../Keyword_resource.txt

*** Test Cases ***
View a domain details
    Given I am a user
    And I go to view domain 1 page
    And I check domain view form    Domain One

Wrong domain Id
    Given I am a user
    And I go to domains list
    When Go to    ${URL}/domain/999
    Then Page Should Contain    404 Not Found

Return to list button
    Given I am a user
    And I go to view domain 1 page
    When I click button    Return to list
    Then Location Should Be    ${URL}/domain/

Edit button for super-admin profile
    Given I am a super-admin
    And I go to view domain 1 page
    When I click button    Edit
    Then Location Should Be    ${URL}/domain/edit/1

Edit button not displayed for non super-admin profile
    Given I am a user
    And I go to view domain 1 page
    Then I should not see action button(s)    Edit

Breadcrumb
    Given I am a user
    When I go to view domain 1 page
    Then Page Should Contain    Admin > View domain

Tab Projects
    Given I am a user
    And I go to view domain 1 page
    And Element Should Contain    id=badge_projects    3
    When Click Link    id=a_projects
    Then Location Should Be    ${URL}/dm-project/

Tab Resources
    Given I am a user
    And I go to view domain 1 page
    And Element Should Contain    id=badge_resources    5
    When Click Link    id=a_resources
    Then Location Should Be    ${URL}/dm-resource/

*** Keywords ***
I go to view domain 1 page
    And I go to domains list
    And I click action button    View domain #1
