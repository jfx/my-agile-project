*** Settings ***
Documentation     In order to manage domain
...               As a super-admin user profile
...               I need to add a domain.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Add a domain
    Given I am a super-admin
    And I go to domains list
    And I click action button    Add
    When I fill in domain form    Dx
    And I save
    Then Page Should Contain    Domain added successfully
    And I check domain view form    Dx
    And I click button    Return to list
    And the table should contain x rows    6
    And the table should contain    Dx    Details 4 Dx

Impossible to add a domain with a name too short
    Given I am a super-admin
    And I go to domains list
    And I click action button    Add
    When Input Text    map_domain_name    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to add a domain with a duplicate name
    Given I am a super-admin
    And I go to domains list
    And I click action button    Add
    When Input Text    map_domain_name    Domain One
    And I save
    Then Page Should Contain    A domain with this name already exists.

Breadcrumb
    Given I am a super-admin
    And I go to domains list
    When I click action button    Add
    Then the breadcrumb should contain    Admin    Add domain

Return to list button
    Given I am a super-admin
    And I go to domains list
    And I click action button    Add
    When I click button    Return to list
    Then Location Should Be    ${URL}/domain/

*** Keywords ***
