*** Settings ***
Documentation     In order to manage resource for a domain
...               As a super-admin user profile or a user with a manager role
...               I need to add a resource.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A super-admin adds a resource
    Given I am a super-admin
    And I go to resources list    View domain #1
    And I click action button    Add
    Select From List By Label    id=map_domainbundle_resourceaddtype_user    No-domain Firstno-domain
    Select From List By Label    id=map_domainbundle_resourceaddtype_role    User+
    When I save
    Then Page Should Contain    Resource added successfully
    And the table should contain x rows    6
    And the table should contain    No-domain Firstno-domain    displayno-domain    User+

A manager adds a resource
    Given I am a manager
    And I go to resources list    View domain #1
    And I click action button    Add
    Select From List By Label    id=map_domainbundle_resourceaddtype_user    No-domain Firstno-domain
    Select From List By Label    id=map_domainbundle_resourceaddtype_role    User+
    When I save
    Then Page Should Contain    Resource added successfully
    And the table should contain x rows    6
    And the table should contain    No-domain Firstno-domain    displayno-domain    User+

A super-admin adds a manager
    Given I am a super-admin
    And I go to resources list    View domain #3
    And I click action button    Add
    Select From List By Label    id=map_domainbundle_resourceaddtype_user    No-domain Firstno-domain
    Select From List By Label    id=map_domainbundle_resourceaddtype_role    Manager
    When I save
    Then Page Should Contain    Resource added successfully
    And the table should contain x rows    1
    And the table should contain    No-domain Firstno-domain    displayno-domain    Manager
    And I log out
    And I am logging as    no-domain
    And I go to resources list    View domain #3
    And I should see action button(s)    Add

Impossible to add a resource without selecting a domain before
    Given I am a super-admin
    When Go To    ${URL}/dm-resource/add
    Then Location Should Be    ${URL}/domain/

Breadcrumb
    Given I am a super-admin
    And I go to resources list    View domain #1
    When I click action button    Add
    Then Page Should Contain    Domain One > Add resource

Breadcrumb domain link
    Given I am a super-admin
    And I go to resources list    View domain #1
    When I click action button    Add
    When Click Link    id=b_domain
    Then Location Should Be    ${URL}/domain/1

Return to list button
    Given I am a super-admin
    And I go to resources list    View domain #1
    And I click action button    Add
    When I click button    Return to list
    Then Location Should Be    ${URL}/dm-resource/
    And Page Should Contain    Domain One
