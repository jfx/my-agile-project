*** Settings ***
Documentation     In order to see details about a specific project
...               As a connected user
...               I need to view project details.
Resource          ../Keyword_resource.txt

*** Test Cases ***
View a project details
    Given I am a user
    And I go to projects list    View domain #1
    When I click action button    View project #3
    Then Location Should Be    ${URL}/project/3
    And Element Text Should Be    id=inputName    Project Closed
    And Element Text Should Be    id=inputStartDate    02/05/2013
    And Element Text Should Be    id=inputFinishDate    31/07/2013
    And Checkbox Should Be Selected    inputClosed

Wrong project Id for a domain
    Given I am a manager
    And I go to projects list    View domain #1
    When Go to    ${URL}/project/4
    Then Location Should Be    ${URL}/project/4
    And Element Text Should Be    id=inputName    Project Three

Wrong project Id
    Given I am a user
    And I go to projects list    View domain #1
    When Go to    ${URL}/project/999
    Then I should see 404 error page

View a project without selecting a domain before
    Given I am a user
    When Go to    ${URL}/project/1
    Then Location should be    ${URL}/project/1
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Edit button for a manager role on domain
    Given I am a manager
    And I go to view project #1 page
    When I click button    Edit
    Then Location Should Be    ${URL}/project/edit/1

Edit button not displayed for a non manager role
    Given I am a user
    And I go to view project #1 page
    Then I should not see action button(s)    Edit

Breadcrumb
    Given I am a user
    When I go to view project #1 page
    Then Page Should Contain    Domain One > Project One > View

Breadcrumb domain link
    Given I am a user
    And I go to view project #1 page
    When Click Link    id=b_domain
    Then Location Should Be    ${URL}/dm-project/

Breadcrumb project link
    Given I am a user
    And I go to view project #1 page
    When Click Link    id=b_project
    Then Location Should Be    ${URL}/project/1

Objectives tab
    Given I am a user
    And I go to view project #1 page
    When Click Link    Objectives
    Then Location Should Be    ${URL}/project/1#

Milestones tab
    Given I am a user
    And I go to view project #1 page
    When Click Link    Milestones
    Then Location Should Be    ${URL}/project/1#

Risks tab
    Given I am a user
    And I go to view project #1 page
    When Click Link    Risks
    Then Location Should Be    ${URL}/project/1#

Return to domain button
    Given I am a user
    And I go to view project #1 page
    When I click button    Return to domain
    Then Location Should Be    ${URL}/dm-project/

*** Keywords ***
I go to view project #1 page
    And I go to projects list    View domain #1
    And I click action button    View project #1
