*** Settings ***
Documentation     In order to manage a project
...               As a user with a manager role
...               I need to edit a project.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A manager edits a project
    Given I am a manager
    And I go to edit project #1 page
    When I fill in project form    P1Mod    0    0
    And I save
    Then Page Should Contain    Project edited successfully
    And I check project view form    P1Mod    -2    1
    And I click button    Return to domain
    And the table should contain x rows    3
    And the table should contain    P1Mod    Details 4 P1Mod

A manager closes a project
    Given I am a manager
    And I go to edit project #1 page
    When Select Checkbox    map_projectbundle_projecttype_closed
    And I save
    Then Page Should Contain    Project edited successfully
    And Checkbox Should Be Selected    inputClosed

A manager opens a closed project
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Edit project #3
    When Unselect Checkbox    map_projectbundle_projecttype_closed
    And I save
    Then Page Should Contain    Project edited successfully
    And Checkbox Should Not Be Selected    inputClosed

Impossible to edit a project with a name too short
    Given I am a manager
    And I go to edit project #1 page
    When Input Text    map_projectbundle_projecttype_name    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to modify a project with wrong start date
    Given I am a manager
    And I go to edit project #1 page
    When Input Text    map_projectbundle_projecttype_startDate    xx/xx/xxxx
    And Click Element    map_projectbundle_projecttype_name
    And I save
    Then Page Should Contain    This value is not valid.

Impossible to modify a project with wrong finish date
    Given I am a manager
    And I go to edit project #1 page
    When Input Text    map_projectbundle_projecttype_finishDate    xx/xx/xxxx
    And Click Element    map_projectbundle_projecttype_name
    And I save
    Then Page Should Contain    This value is not valid.

Impossible to modify a project with a finish date before a start date
    Given I am a manager
    And I go to edit project #1 page
    When I fill in project form    P1Mod    6    0
    And I save
    Then Page Should Contain    The finish date must be after the start date.

Wrong project Id for a domain
    Given I am a manager
    And I go to projects list    View domain #1
    When Go to    ${URL}/project/edit/4
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}

Wrong project Id
    Given I am a manager
    And I go to projects list    View domain #1
    When Go to    ${URL}/project/edit/999
    Then Page Should Contain    404 Not Found

Edit a project without selecting a domain before
    Given I am a manager
    When Go to    ${URL}/project/edit/1
    Then Location should be    ${URL}/project/edit/1
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Breadcrumb
    Given I am a manager
    When I go to edit project #1 page
    Then Page Should Contain    Domain One > Project One > Edit

Breadcrumb domain link
    Given I am a manager
    And I go to edit project #1 page
    When Click Link    id=b_domain
    Then Location Should Be    ${URL}/dm-project/

Breadcrumb project link
    Given I am a manager
    And I go to edit project #1 page
    When Click Link    id=b_project
    Then Location Should Be    ${URL}/project/1

Objectives tab
    Given I am a manager
    And I go to edit project #1 page
    When Click Link    Objectives
    Then Location Should Be    ${URL}/project/edit/1#

Milestones tab
    Given I am a manager
    And I go to edit project #1 page
    When Click Link    Milestones
    Then Location Should Be    ${URL}/project/edit/1#

Risks tab
    Given I am a manager
    And I go to edit project #1 page
    When Click Link    Risks
    Then Location Should Be    ${URL}/project/edit/1#

Return to domain button
    Given I am a manager
    And I go to edit project #1 page
    When I click button    Return to domain
    Then Location Should Be    ${URL}/dm-project/

*** Keywords ***
I go to edit project #1 page
    And I go to projects list    View domain #1
    And I click action button    Edit project #1
