*** Settings ***
Documentation     In order to manage a project
...               As a user with a manager role
...               I need to delete a project.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A manager deletes a project
    Given I am a manager
    And I go to delete project page    2
    When I click button    Remove
    And I confirm
    Then Location Should Be    ${URL}/dm-project/
    And Page Should Contain    Project removed successfully
    And Page Should not Contain    Project Two

Cancel to delete a project
    Given I am a manager
    And I go to delete project page    2
    When I click button    Remove
    And I do not confirm
    Then Location Should Be    ${URL}/project/del/2

Impossible to delete a project
    [Tags]    failedTest
    Given I am a manager
    And I go to delete project page    1
    When I click button    Remove
    And I confirm
    Then Location Should Be    ${URL}/project/del/1
    And Page Should Contain    Impossible to remove this item - Integrity constraint violation !

Wrong project Id for a domain
    Given I am a manager
    And I go to projects list    View domain #1
    When Go to    ${URL}/project/del/4
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}

Wrong project Id
    Given I am a manager
    And I go to projects list    View domain #1
    When Go to    ${URL}/project/del/999
    Then I should see 404 error page

Delete a project without selecting a domain before
    Given I am a manager
    When Go to    ${URL}/project/del/1
    Then Location should be    ${URL}/project/del/1
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Breadcrumb
    Given I am a manager
    When I go to delete project page    1
    Then Page Should Contain    Domain One > Project One > Delete

Breadcrumb domain link
    Given I am a manager
    And I go to delete project page    1
    When Click Link    id=b_domain
    Then Location Should Be    ${URL}/dm-project/

Breadcrumb project link
    Given I am a manager
    And I go to delete project page    1
    When Click Link    id=b_project
    Then Location Should Be    ${URL}/project/1

Return to domain button
    Given I am a manager
    And I go to delete project page    1
    When I click button    Return to domain
    Then Location Should Be    ${URL}/dm-project/

*** Keywords ***
I go to delete project page
    [Arguments]    ${id}
    And I go to projects list    View domain #1
    And I click action button    Delete project #${id}
