*** Settings ***
Documentation     In order to manage a project
...               As a user with a manager role
...               I need to add a project.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A manager adds a project
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When I fill in project form    P1x
    And I save
    Then Page Should Contain    Project added successfully
    And I check project view form    P1x
    And I click button    Return to domain
    And the table should contain x rows    4
    And the table should contain    P1x    Details 4 P1x

A manager adds a closed project
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When I fill in project form    P1x    0    3    True
    And I save
    Then Page Should Contain    Project added successfully
    And I check project view form    P1x    0    3    True
    And I click button    Return to domain
    And the table should contain x rows    4
    And the table should contain    P1x    Details 4 P1x

Impossible to add a project with a name too short
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When I fill in project form    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to add a project with wrong start date
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When I fill in project form    P1x
    And Input text    map_projectbundle_projecttype_startDate    xx/xx/xxxx
    And Click Element    map_projectbundle_projecttype_name
    And I save
    Then Page Should Contain    This value is not valid.

Impossible to add a project with wrong finish date
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When I fill in project form    P1x
    And Input text    map_projectbundle_projecttype_finishDate    xx/xx/xxxx
    And Click Element    map_projectbundle_projecttype_name
    And I save
    Then Page Should Contain    This value is not valid.

Impossible to add a project with a finish date before a start date
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When I fill in project form    P1x    6    0
    And I save
    Then Page Should Contain    The finish date must be after the start date.

Impossible to add a project without selecting a domain before
    Given I am a manager
    When Go to    ${URL}/project/add
    Then Page Should Contain    403 Forbidden

Return to domain button
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When I click button    Return to domain
    Then Location Should Be    ${URL}/dm-project/

Domain link
    Given I am a manager
    And I go to projects list    View domain #1
    And I click action button    Add
    When Click Link    Domain One
    Then Location Should Be    ${URL}/dm-project/
